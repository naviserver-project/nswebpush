package require tcltest
package require json

#
# For testing purposes, we use the module source directory, the place
# where "make test" is to be executed. Furthermore, we use for testing
# always the same key pair, which is taken from the demo directory.
#
set scriptDir [pwd]/..
source $scriptDir/webpush-procs.tcl
ns_log notice "script $scriptDir/webpush.tcl was loaded"

set ::vapidCertPath $scriptDir/demo

#
# This is the endpoint this script sends 3 notifications to for testing
#
set ::testEndpoint {
    {"endpoint":"https://fcm.googleapis.com/fcm/send/d9IxzALjkyU:APA91bFkfLpZ4H8TEaCel9OtfL0V1LCSOGg253Cb0829sSwfo2CcKt_neqG5mZy8fhhRoeYyj-3Ds8i3edWn06snJxOULw68QV0RQe6yOgJ7vF70LWvzVi2QP1ZxtBLwLLkbbfI62Yuj","expirationTime":null,"keys":{"p256dh":"BG8SsZZCcU-hqyygt5d3Ov39GEyFM6wixAJqz37KBdEibeSKwJZz_T3li6B1aktuYDMOA0fEqIhzzMLqGfQ3gNU","auth":"8O1T8AvRJcqA8UB3vMVCoA"}}
}

namespace eval webpush {
    namespace import ::tcltest::*

    ##########################################################################
    # testing dict transformation
    ##########################################################################

    test dictToJson-1.0 {} -body {
        # key and value of json are supposed to be quoted
        set d [dict create a b 1 4]
        set result [dictToJson $d]
        set d [dict create "a" b 1 "4"]
        append result [dictToJson $d]
        # whitespaces in keys/values should stay
        set d [dict create "a 1" a b "b 2"]
        append result [dictToJson $d]
    } -result {{"a":"b","1":"4"}{"a":"b","1":"4"}{"a 1":"a","b":"b 2"}}


    ##########################################################################
    # claim validation
    ##########################################################################

    test validateClaim-1.0 {} -body {
        set validMail {mailto:georg@test.com}
        # this is only a valid formatting, the endpoint does not exist
        set validEndpoint "https://updates.push.services.mozilla.com/wpush/v2/gAAAAABa6CXAoHisP"

        set claim [validateClaim [subst {sub $validMail exp 12345}] $validEndpoint]
        set exp [dict get $claim exp]
        set result [expr {[clock seconds] < $exp && $exp < [clock seconds] + 60*61*24}]

        set claim [validateClaim [subst {sub $validMail}] $validEndpoint]
        if {![catch {dict get $claim exp}]} {
            append result 1
        }

        set claim [validateClaim [subst {sub $validMail exp [expr {[clock seconds] + 60*60*27}]}] $validEndpoint]
        set exp [dict get $claim exp]
        append result [expr {[clock seconds] < $exp && $exp < [clock seconds] + 60*61*24}]
        set aud [dict get $claim aud]
        if {$aud eq "https://updates.push.services.mozilla.com"} {
            append result 1
        }
        # endpoint and 'aud' mismatch
        append result [catch {validateClaim [subst {sub $validMail aud "abc"}] $validEndpoint}]
    } -result {11111}


    ##########################################################################
    # Tests for webpush::send
    ##########################################################################

    test webpush-send-1.0 {various exceptions} -body {

        set validMail {mailto:georg@test.com}
        # this is only a valid formatting, the endpoint does not exist
        set validEndpoint {endpoint https://updates.push.services.mozilla.com/wpush/v2/gAAAAABa6CXAoHisP}
        set validPem $::vapidCertPath/prime256v1_key.pem

        # all wrong
        set result [catch {
            webpush::send -subscription a -data "" -claim "" -privateKeyPem "" -localKeyPath ""
        }]

        # missing private key
        append result [catch {webpush::send \
                                  -subscription $validEndpoint \
                                  -data "" \
                                  -claim [subst {sub $validMail}] \
                                  -privateKeyPem "" \
                                  -localKeyPath $::vapidCertPath}]

        # private key not a pem file
        append result [catch {webpush::send \
                                  -subscription $validEndpoint \
                                  -data "" \
                                  -claim [subst {sub $validMail}] \
                                  -privateKeyPem $::vapidCertPath/public_key.txt \
                                  -localKeyPath $::vapidCertPath}]

        # invalid email address
        append result [catch {webpush::send \
                                  -subscription $validEndpoint \
                                  -data "" \
                                  -claim {sub maito:testtest} \
                                  -privateKeyPem $validPem \
                                  -localKeyPath $::vapidCertPath}]

        # auth and p256dh missing in subscription for data bearing Webpush
        append result [catch {webpush::send \
                                  -subscription $validEndpoint \
                                  -data "testdata" \
                                  -claim [subst {sub $validMail}] \
                                  -privateKeyPem $validPem \
                                  -localKeyPath $::vapidCertPath}]

        # invalid GCM mode
        append result [catch {webpush::send \
                                  -subscription $validEndpoint \
                                  -data "" \
                                  -claim [subst {sub $validMail}] \
                                  -privateKeyPem $validPem \
                                  -localKeyPath $::vapidCertPath \
                                  -mode abcencoding}]
    } -result {111111}

    test webpush-send-1.1 {cannot connect} -body {
        set validMail {mailto:georg@test.com}
        # this is only a valid formatting, the endpoint does not exist
        set validEndpoint {endpoint https://updates.push.services.mozilla.com/wpush/v2/gAAAAABa6CXAoHisP}
        set validPem $::vapidCertPath/prime256v1_key.pem
        # all good (no data is ok) - expected result is error 404 cannot connect

        catch {webpush::send \
                   -subscription $validEndpoint \
                   -data "" \
                   -claim [subst {sub $validMail}] \
                   -privateKeyPem $validPem \
                   -localKeyPath $::vapidCertPath
        } msg opt
        set result [dict get $opt -errorcode]

        # all good (valid aud)
        catch {webpush::send \
                   -subscription $validEndpoint \
                   -data "" \
                   -claim [subst {sub $validMail aud "https://updates.push.services.mozilla.com/"}] \
                   -privateKeyPem $validPem \
                   -localKeyPath $::vapidCertPath
        } msg opt
        append result [dict get $opt -errorcode]

        # all good (valid exp)
        catch {webpush::send \
                   -subscription $validEndpoint \
                   -data "" \
                   -claim [subst {sub $validMail exp [expr {[clock seconds] + 60*120}]}] \
                   -privateKeyPem $validPem \
                   -localKeyPath $::vapidCertPath
        } msg opt
        append result [dict get $opt -errorcode]
    } -result {404404404}


    test webpush-send-1.2 {send successfully} -body {
        set epDict [::json::json2dict $::testEndpoint]
        set keys [dict get $epDict keys]
        set validEndpoint [subst {endpoint [dict get $epDict endpoint] auth [dict get $keys auth] p256dh [dict get $keys p256dh]}]
        set validClaim {sub mailto:georg@test.com}
        set validPem $::vapidCertPath/prime256v1_key.pem
        set result {}
        #ns_logctl severity Debug(task) on
        #ns_logctl severity Debug(request) on
        #ns_logctl severity Debug(driver) on
        #ns_logctl severity Debug on

        if {[webpush::send \
                 -subscription $validEndpoint \
                 -data "" \
                 -claim $validClaim \
                 -privateKeyPem $validPem \
                 -localKeyPath $::vapidCertPath \
                 -mode aesgcm \
                 -timeout 60] < 300} {
            lappend result 1
        }
        #ns_log notice "======================== 1 DONE"
        if {[webpush::send \
                 -subscription $validEndpoint \
                 -data "encrypted data received!" \
                 -claim $validClaim \
                 -privateKeyPem $validPem \
                 -localKeyPath $::vapidCertPath \
                 -mode aesgcm \
                 -timeout 60] < 300} {
            lappend result 1
        }
        #ns_log notice "======================== 2 DONE"
        if {[webpush::send \
                 -subscription $validEndpoint \
                 -data "aes128gcm data received!" \
                 -claim $validClaim \
                 -privateKeyPem  $validPem \
                 -localKeyPath $::vapidCertPath \
                 -mode aes128gcm \
                 -timeout 60] < 300} {
            lappend result 1
        }
        #ns_log notice "======================== 3 DONE"
        set result

    } -returnCodes {ok error} -result {1 1 1}


    ##########################################################################
    # PEM tests
    ##########################################################################

    test pem-1.0 {createTmpPrivateKeyPem} -body {
        set pemFile [createTmpPrivateKeyPem]
        if {[file exists $pemFile]} {
            set result 1
        }
    } -result {1}


    # input/output for encryption functions from node.js crypto library
    # see "test.js"
    test generateInfo-1.0 {} -body {
        set result [generateInfo aesgcm \
                        [ns_base64urldecode -binary BJkXi48PlCiNCs9dLggxXQ39bdi64agt_emycss5gsg5BYqOWwP5gnbmga7Rg1_tKvnu0c3InK0C850s1czzyBg] \
                        [ns_base64urldecode -binary BJZRgas6kMag9rP2X5oVVhGzPwwT24p103WKkPlB7jFTmYVA3QsuLBaSSxNO-UVU-0SjHo0uIsiNoFQRYLDt7cE]]
        set result [string map {"\n" {}} [ns_base64encode $result]]

    } -result {Q29udGVudC1FbmNvZGluZzogYWVzZ2NtAFAtMjU2AABBBJkXi48PlCiNCs9dLggxXQ39bdi64agt/emycss5gsg5BYqOWwP5gnbmga7Rg1/tKvnu0c3InK0C850s1czzyBgAQQSWUYGrOpDGoPaz9l+aFVYRsz8ME9uKddN1ipD5Qe4xU5mFQN0LLiwWkksTTvlFVPtEox6NLiLIjaBUEWCw7e3B}


    ##########################################################################
    # encryption tests
    ##########################################################################

    test encrypt-aesgcm-1.0 {
        createEncryptionKeyNonce for aesgcm
    } -body {
        set clientPubKey [ns_base64decode -binary BNvOAPaPCCfpNCMR4AccTffxD8YMQbfIBWieLxgZE1qgU+YVVT5mOD7CaRRqg5ykA7/f8jm2VuOPZLvHn0moHas=]
        set serverPubKey [ns_base64decode -binary BFzhXP5G5Pp5xmEfESPsd7L6N2oQZZypGd2tUR5diW9spzJFs5DXaUuM1iMVfZGunUhtHkyYjqPfcQ2bfzKzbeY=]
        set ikm [ns_base64decode -binary 4qL0g1tKiepxN01MPiRVjDAgC8PWwjlpFNccAS5rtvo=]
        set salt [ns_base64decode -binary WVGtEt/7tGKMNgqAeDvEPA==]
        lassign [createEncryptionKeyNonce \
                     $clientPubKey \
                     $serverPubKey \
                     $ikm \
                     $salt \
                     aesgcm] key nonce
        list [ns_base64encode $key] [ns_base64encode $nonce]
    } -result {bwUz6s4vfAi5a9xGFBVRXg== n14o2v4+edg7+ggO}



    test encrypt-aesgcm-1.2 {
        full encryption aesgcm
    } -body {
        set auth   [ns_base64urldecode -binary 4LLU4S9l1S9IrPTsQZkPqw]
        set p256dh [ns_base64urldecode -binary BNvOAPaPCCfpNCMR4AccTffxD8YMQbfIBWieLxgZE1qgU-YVVT5mOD7CaRRqg5ykA7_f8jm2VuOPZLvHn0moHas]
        set salt   [ns_base64decode -binary WVGtEt/7tGKMNgqAeDvEPA==]
        set result [encrypt -data "Push notification payload!" \
                        -privateKeyPem $::vapidCertPath/prime256v1_key.pem \
                        -auth $auth\
                        -p256dh $p256dh \
                        -salt $salt \
                        -mode aesgcm]
        #ns_log notice "obj(encrypt) [nsf::__db_get_obj $result]"
        #ns_log notice "encrypted [string length $result]"
        set result [string map {"\n" {}} [ns_base64encode $result]]
        catch {encrypt \
                   -data [string repeat A 4079] \
                   -privateKeyPem $::vapidCertPath/prime256v1_key.pem \
                   -auth $auth \
                   -p256dh $p256dh \
                   -salt $salt \
                   -mode aesgcm} errorMsg
        lappend result $errorMsg
    } -result {nCl1FvcUQnI324wU2MuLs+nEIs4ns0vAI3Dz2pJk2HQ/2HUI3IV12siYn8leeC/53dV7PBwO0wmNhyBVsWg31ZyGbLwYW9rWzKSKbHrkRowvq1hdEnuQGYdIfff+cHylWHzZ241oAV5jNtz4+ojN4oqlAQfPbz+qZaQTZATvHQ6rEsgcjvRi1mIe4YiJDdlSC7IQI0oJEtaODjNprBBfKw6uOzMqYerNGEZ6UaJAsi+4/VuAOh8B2MM4AI9cVwcjZmoZrotv5/dpu5FL1cCR4TXV7a5cPwjXGjl7b0I6zmKEQzhpuiRFKHZs84XPymgCQsCh+r06E5Isd5NRJ9JJknk68sC/I/W4vBtBGLp8rvOddfMgUg0tx+uV9F0G0Jg72wM48gBDVbEMkTtO+Ww6Wr4+ylglXLiwhzxqcfuN9KLAWGugPAUGAmkDLDkuUatvhTWDPeQAbEY9yWbZn8o313wDm242BpbRY/0BF2pdRFd1N9EnOF+RTXMRY0IDBCPtuBVsUmRZ73FFM5JFoburh9Q+jJruWbLX+ZaRLiKGsZ25ETsqc4hNc7oiNAOShTwuCDPIODeCq1irq67gALsFJLemoOUG+jav6szfooBkWc1HfqyF3g69fQ9QLeEBUnGgvZ8pPGM0cDd9Ti0YkqXopJuHwzOsB0yt2+lIXo4HktmV8mvTNn6p9BuH/jbZTNxRAPqzZWzQoqrwH2MZylmcnfxb6/sb5dIxbA7ksBna6xAkav1srXtxmer9dSXPFzEePAp1NrVvV8ts4Fb3PoqknOGtSf//brVOSQanmJcmDru+ix4WX7SBGYWKyRPFHZJ/x8vlxA1GdMSVVdTAbfalFiuN2NE2ZIk+PdD2ToqE7xHhPIh2W1JSTnBuQvycadDxAzlDQ7SG4RAWjHZ2yZbpIuTXgXpGAM51LU7Ys/1eSXA28Iw+KCxjiXKVkDN2qOK7oXHYTPPkAgrrNVaYr5QJRiF2zu8EU2RUXRxCX0TqoTD83qTQNjy8zxctMCyQjbbFa8jVc8jUlE1q93SYNo54gnyU9g1mFQWvmGBswsqXgkmQAzkTO7XCeIZFoStSekeR+JwbnbjcxbFnDEQ129wNprVoRhwAXzFzMCFY1EpMPhLZdgHi31j/8bAdElftrBGRmONmjInEBl5p+bFeTuDMb+NY0XSb0Q99dQCLCgS50rcoyvYernk0kYFUL6y1ul76nolesEpeRV2VweG3FCJm+xJxPvY/IaFCb6iTHgcLUums2+gnQTJTpG9fSpi+DiHNcCqsWXXP3128c6K6SGF20r/8DykPJuUVjmDZwZJlF43qSUiB/dNfcTZPRnq+FNbZ2YMPUD9Hg0WyHQmhLtInqurXwIz3a2nqDKHe7MUM28qzQaLYicUUsJoWyw0jpY1X8IwUIBHlcT0H/p2KzjbeoZBWr+/nyBA5Fxfb+rIPI+XFRm4hDNHEMkJVEb0wkjJOzYIKU0B4Y57VfOU20tRvfQcN+zNnEFJ8TqdOpnP5fZ+89hAWtydH/SdEEh1Y2YSghQdvCfRaHX+LddsCYtZHC2OKjsuSjQ97jsq5z0B/0zb36dnSHgq/bayRK7YGrYhJJen0l32uzzrJPqkeLokvKO11swUpflQC7FmIo813YsFYXbeP38kctGN+lbGRoDP4JtnXiRGhWbJ1+zS9I//Tcb9LPGUSoBiWlLLIgH0Iy0xzAxkpG0eqXnUP84jI40t5n4j58P8qyF9e6YST47LxsrqvyhrNVs+OzzKW/pc12MK6DC2ng3XJf8VBsrTWMsN3RCfiCcW7Sh4La0z2W7MmjVedjxKVXV30rFeasqNWJTfbQg8FmdliVd9SjIUHnvYrFuLqSpsVAOMF4daFP2avM77CSvEgOz2oR6877EbnbYCl8UosRGRHIamZDiOq+wG1ELp45kesRJMB0X2JMZMRYpfOcPNh/OhBUbWw/r6Xq5qA0O61tUGvddlesP9vRAy+3spxKKK87fuXUILsbvHOfU/DfKOhjb94JI0fVk3hNs+OKxqaqqXteda2z73CSOgVsBv/iYQYMVoreOxXSBUUiz/Z+tWR0bTjwELZ2NPUZXI7tVNc7EGzu/wA20TaB8VFSXhq85QghDDdGap+A2GDc3q7ir9Lu+YT0OjJ4wOG2azKNTsGF5xpNrh1zN0SWvMVqfxnJ04YzpFai8P4rcPvk64sZ3Ld9r5CIDC8PKsPQlU7Lagb67P9BaBzsKCJRMJUAbVjz2sn44moiphStXSIlu0DmnYE6MUi3Hv7fkZHZ+A9cP8r3szy0WlMvXhxUfym4YLU8gkX+BkkCqSVNO+xYI9XuUdDagHS10Lon+V0CvuzFhTCVEZiC/wlUYtSJZ2PtNqRKdgdWCTlEKNkCoYGYGmNS1QYinUwkcmHNVtBvTnHAxJb5vxv2PaKmhSROp4kJj/zTjJMOk7GgnBu7OUpFxNJiB6OqjW/guPZw+bRWPIR5XT2RWBhbOJUd09msAcS5+5KdXgBhvFczXe8aWmjq87LtDoMDZ1IeWdKudubJPPpUNWr11GobGCa2EqlSsJaQGsMi/rmRHakky3wdXkd35ZHeC2dbni6gNVUzRF9acW+dVU8XDHd3Vhh5yCvyo3QwsyTZL9/fbNOiWQ6e06uMVGdZlR+qIaJzCgg2EZN9XSorh7wBQg8miP2BNMheMkdjq1/46iojTbMwarO9rNTnMsIUgjNXAlfR2nQirxIEPo5s1fgEXQ2cO4kHtJruwzCubEr7vHLsF5ik/ekLCJXEH28muhLcaid+EXyZfp2U+qmOjpvmXnY7OxDHv9A/o+XP4z4ZA44THHNbWY8BUotBMhuWhc4p+2eib4oXWivsuLCvX4Ekz7Dzz3/NaBZx6azRnf6FML00YkWcuMsFIpDslFunXVH/NbloACtQ8H/aT6S/N+8yX5An1GigzYuI/Yp5Q81GRDXhFZhPDs4+836ivtj2/3yR0BZOWOMdl9XFoROTLX05tN4t4qtsVwDeCpG42t+resHAikN8IrywSc9eoHsLEjimNeUSZx5W36cq4cFHXcRr7tfqemOYjfUGZsoTANIZqLEtyfavo5J1KyZ2JCshzFZT6SQX/0vw6kEBpc+A1PqzomuAk8fj5bAMZqBb2APz3VzLdulGTgOoj7O9vAPSZvcuYNyMKrvAiGwQW6GMkoFFza0KRhZyxAixwMz1PJ9TtcOgvIjTNAHbMavUtcQdQMiPydQQviJk1zACIlOtE0kYPbtMrjDi49ffGUFrvTYLNDZ1cvwaLhVENdySSYolEHMFsfQMLN8E+vE6vJCkqvvVdbRwhqhEZCJKpHI74ePkUT69iAhH7Khb5mfe8MAUwVgeqOUvjCMhE78KSUqZRH/wRCZhF7SrEBBwYygkNcdMjZ1Ch8OSR+WsiYRcODDOh01qAKx94bGBeh1qjudwA46YGm9jbKLu+JAEhRb+vmSR2AA2Tr+6NCweZdICf7plKYqH4LONA7HRGKPp/LCCyAcmDAgATo76bKaKoM5ABdKMWQB0ltpkH3WxZzq32dE3tRCOJ4sKJnRFAwfW3MsbmVSNcK7DG6/PE+Ll+CYjpmjomVgy43mE3OfNEox7OFLu0w4n9pDrDI4DGc1XIOcGXPowbFLD4TiRTlrAjr/KBuwGV7pPyo68SwIR7u2HQFI79s75ZDLoFaf+O5nUB5q+ZPbqwg1HrMRmdh0KhLxVBITQ6VEzK8IymQTLf/+PnHj+D2owj7TtY7Lk1pqFfkvngC2ceXaclYayhrHzt7erFHPofdQ0tc2+5Bkhvpy22IJol/NMvcdDj+8+/JXzv/YBeXspHUnBji9xQvoC9ofP+LSle9me1Zyk3G5bPADkVb6dIJoRefKsog3Igs+/1hN87Nv+JWT3cwVCB+tVGnd+7fQeNApoZ7Xf+YpE0pfo+9tNsfp3dtDNwnFYMTsIwccsWzl35WP3zv+EZj2IfehbmNwH5qm0lakPWjS/6+I52ucmidyAb4Pf8Mit5wzhSn/8H84FTiFIWIrYFyZSD5ZuPW7COT4TKQK0+Eg9+OO3aonJKCoPFETW8HYAYWFTktI/GPNfrZlT+lXu76RBrpuZZrcjx7POHuT9W0lC9SfvMz2KvOxK3WeWp8RC7C1h/BHpnyws0pZiQvWhbjpsZoGjVGFgFmWP97g/smWcItVH7qjy9/WlVcAl00qT1WpF2B2BqlBH4mcH8iR4ApVuHEXl4NwEjRXwumeBKaYoSD+8k83cSnBxU2zNfYt+leTxMJJER4vYIegoMZqOdubGurt7erpfwWJ+iZzyWo61C2B+SETzPyLH2tHrSsjqOEnA9Dk3YA0t0qWonWoKZNTpFE+mB1JpLKEdTqVSJZcinJp0FoMeqXO9iIBOgYIH8D6EU0Y6ItX4WTsct4C/8r5/yfgcxzm5zUKutlfadGV71xrTyLQ43Mx6FhgcPsqgzqxEl2GYJ7c3ADGvjmVrtPpUTOur19BfRD7wftPdysCKOWQoUpawTv3Q41/4llZYD/ajDhwkvs8/xaYCbqj/chbdfkYU3g+K/7Od4qXvGOLznziy0C36z8dyDmTjkCU+K52U9ig3WToLxwvbLo+kLmPR+Zc9s98452kxwm692TijSbwcsBbd3MDLjAdsvCDHKOtvqJEYwyYxs+G6Kxcopx8vyyXo6QxzOOGdT3FJn+2bQlCq5sPaxv3JBDJjmmIhBXcx74bjNNIcWGyJeW7tuhewwQkC1kMPybFacwAc5y+X6vNmioo7gYDIR/cx348LsG0vPIMWYJhvN3Fa2s3yIrTLvWOBIqtTTxfjmF9epCMGDNsoU4uHI6FdosYOp5MvJjfwXoohR7D5RZB9WrKuguv+sX5bokYSfTP8N2s0DFU0xlHn5Z/qFErcYZpPQ9HeB48Ktyg9753MBICsqj22GocO2etru9AhCEO28o0njl7P4zUYzEz04x+zzWBHIkGJi6xujEQ6ziMwzxcGD7R9LSp5WDFVHxAs7UOfheSx/pvDyORF7L5uO6Uq897XapYgd6g6AAFZQacWsvxpGrkn7mmU9H4nD6L873NeIzGaaVplpOfxszQmYTV+MTN5BWwTGZYc7nxQaiLNtZ1a+2eFWZBabm8kzLIKRBBBElLacsM59MkEKzfOwhjUBXVg/aHGWzUuudQ2+N7vbaWKYITVUj5lLGZ6YbcdTwob6nyJ2NCyA0JGwfRjeqgfJpciZfA3LMT3j5EDU+9xRtX1sPDjaNsOn2yeOZSwAM2nn3t3vVR6xomu3kqtH3MVmKPzvA5IMmcRRJQSqGRUailvvEq5ZOq61V9RO56eSF3kKsoa1NIJMqShHIQmDMtD8QhY8LhsXgwdQL2JEQUzduQQDfPTf5zxBqALBPX7HRdtx5ZYVbLmN/t2JMMKqdnbAndQ0IbqabhGHWp1v/P2/z24E/ubikZ8PkLCkZemBIuYySbmhl4lGbEbQ== {data is too large, maximum is 4078 bytes!}}

    test encrypt-aes128gcm-1.1 {
        createAes128gcmHeader for aes128gcm
    } -body {
        ns_base64urlencode [createAes128gcmHeader \
                                [ns_base64urldecode -binary I1BsxtFttlv3u_Oo94xnmw] \
                                ""]
    } -result {I1BsxtFttlv3u_Oo94xnmwAAEAAA}


    ##########################################################################
    # decryption tests
    ##########################################################################

    test decrypt-aesgcm-1.0 {
        full encrypt and decrypt rountrip
        using aesgcm
    } -body {
        set data "Encryptiontest"
        # this is the client keypair
        set localPrivFn [createTmpPrivateKeyPem]
        set localPub    [ns_crypto::eckey pub -pem $localPrivFn -encoding binary]
        set auth        [ns_base64urldecode -binary 4LLU4S9l1S9IrPTsQZkPqw]
        set salt        [ns_base64decode -binary WVGtEt/7tGKMNgqAeDvEPA==]

        # client public key iś the p256dh field in a subscription
        set encrypted [encrypt -data $data \
                           -privateKeyPem $::vapidCertPath/prime256v1_key.pem \
                           -auth $auth \
                           -p256dh $localPub \
                           -salt $salt \
                           -mode aesgcm]
        #ns_log notice "obj(encrypted) [nsf::__db_get_obj $encrypted]"

        set serverPubKey [ns_crypto::eckey pub -pem $::vapidCertPath/prime256v1_key.pem -encoding binary]

        set result [decrypt -encrData $encrypted \
                        -privateKeyPem $localPrivFn \
                        -auth $auth \
                        -serverPubKey $serverPubKey \
                        -salt $salt \
                        -mode aesgcm]
        set result
    } -cleanup {
        file delete $localPrivFn
    } -returnCodes {ok error} -result {Encryptiontest}

    test decrypt-aesgcm-1.1 {
        Full encrypt and decrypt with UTF-8 characters
        using aesgcm
    } -body {
        set data [encoding convertto utf-8 "test with UTF-8: äüö - 10 € and a black sun ☀"]
        # this is the client keypair
        set localPrivFn [createTmpPrivateKeyPem]
        set localPub    [ns_crypto::eckey pub -pem $localPrivFn -encoding binary]
        set auth        [ns_base64urldecode -binary 4LLU4S9l1S9IrPTsQZkPqw]
        set salt        [ns_base64decode -binary WVGtEt/7tGKMNgqAeDvEPA==]

        # client public key iś the p256dh field in a subscription
        set encrypted [encrypt -data $data \
                           -privateKeyPem $::vapidCertPath/prime256v1_key.pem \
                           -auth $auth \
                           -p256dh $localPub \
                           -salt $salt \
                           -mode aesgcm]
        #ns_log notice "obj(encrypted) [nsf::__db_get_obj $encrypted]"

        set serverPubKey [ns_crypto::eckey pub -pem $::vapidCertPath/prime256v1_key.pem -encoding binary]

        set result [decrypt -encrData $encrypted \
                        -privateKeyPem $localPrivFn \
                        -auth $auth \
                        -serverPubKey $serverPubKey \
                        -salt $salt \
                        -mode aesgcm]
        set result [encoding convertfrom utf-8 $result]
    } -cleanup {
        file delete $localPrivFn
    } -returnCodes {ok error} -result {test with UTF-8: äüö - 10 € and a black sun ☀}


    test decrypt-aes128gcm-1.0  {
        full encrypt and decrypt rountrip
        using aes128gcm
    } -body {
        set data "Encryptiontest"
        # this is the client keypair
        set localPrivFn [createTmpPrivateKeyPem]
        set localPub    [ns_crypto::eckey pub -pem $localPrivFn -encoding binary]
        set auth        [ns_base64urldecode -binary 4LLU4S9l1S9IrPTsQZkPqw]
        # client public key iś the p256dh field in a subscription
        set encrypted [encrypt -data $data \
                           -privateKeyPem $::vapidCertPath/prime256v1_key.pem \
                           -auth $auth \
                           -p256dh $localPub \
                           -salt [ns_base64decode -binary WVGtEt/7tGKMNgqAeDvEPA==] \
                           -mode aes128gcm]

        set result [decrypt -encrData $encrypted \
                        -privateKeyPem $localPrivFn \
                        -auth $auth \
                        -mode aes128gcm]
    } -cleanup {
        file delete $localPrivFn
    } -result {Encryptiontest}

    test decrypt-aes128gcm-1.1  {
        full encrypt and decrypt rountrip with UTF-8 characters
        using aes128gcm
    } -body {
        set data [encoding convertto utf-8 "test with UTF-8: äüö - 10 € and a black sun ☀"]

        # this is the client keypair
        set localPrivFn [createTmpPrivateKeyPem]
        set localPub    [ns_crypto::eckey pub -pem $localPrivFn -encoding binary]
        set auth        [ns_base64urldecode -binary 4LLU4S9l1S9IrPTsQZkPqw]
        # client public key iś the p256dh field in a subscription
        ns_log notice "obj(data) [nsf::__db_get_obj $data] string length [string length $data]"
        set encrypted [encrypt -data $data \
                           -privateKeyPem $::vapidCertPath/prime256v1_key.pem \
                           -auth $auth \
                           -p256dh $localPub \
                           -salt [ns_base64decode -binary WVGtEt/7tGKMNgqAeDvEPA==] \
                           -mode aes128gcm]
        ns_log notice "obj(encrypted) [nsf::__db_get_obj $encrypted] string length [string length $encrypted]"

        set result [decrypt -encrData $encrypted \
                        -privateKeyPem $localPrivFn \
                        -auth $auth \
                        -mode aes128gcm]
        set result [encoding convertfrom utf-8 $result]

    } -cleanup {
        file delete $localPrivFn
    } -result {test with UTF-8: äüö - 10 € and a black sun ☀}

    cleanupTests
}

# Local variables:
#    mode: tcl
#    tcl-indent-level: 4
#    indent-tabs-mode: nil
# End:
